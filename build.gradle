plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'idea'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    mavenCentral()
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'Latvian Maven'
        url = 'https://maven.latvian.dev/releases/'
    }
    maven {
        name = 'KubeJS Maven'
        url = 'https://maven.shedaniel.me/'
    }
    maven {
        name = 'Architectury Maven'
        url = 'https://maven.architectury.dev/'
    }
}

base {
    archivesName = "${mod_id}-${minecraft_version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: 'parchment', version: "${parchment_mappings_version}-${parchment_minecraft_version}"

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--nogui'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    shadow
    localRuntime
    implementation.extendsFrom shadow
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("dev.latvian.mods:kubejs-forge:${kubejs_version}")

    shadow "com.squareup.okhttp3:okhttp:4.12.0"
    shadow "com.google.code.gson:gson:2.10.1"
    shadow "com.squareup.okio:okio:3.6.0"

    shadow("net.dv8tion:JDA:5.1.2") {
        exclude group: 'org.slf4j'
        exclude group: 'ch.qos.logback'
        exclude group: 'net.java.dev.jna'
        exclude group: 'com.sun.jna'
        exclude group: 'com.google.code.findbugs'
        exclude group: 'javax.annotation'
        exclude group: 'org.jetbrains'
        exclude group: 'com.google.errorprone'
        exclude group: 'com.google.guava'
    }

    shadow("org.apache.commons:commons-collections4:4.4")
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version          : forge_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
            kubejs_version         : kubejs_version
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata

// Validate that every dependency declared in mods.toml has the required fields
// before the resources are packaged into the jar. This prevents publishing
// malformed metadata such as dependency entries without the mandatory flag.
def validateModMetadata = tasks.register("validateModMetadata") {
    dependsOn generateModMetadata
    def generatedModsToml = layout.buildDirectory.file("generated/sources/modMetadata/META-INF/mods.toml")
    inputs.file(generatedModsToml)
    doLast {
        def modsToml = generatedModsToml.get().asFile
        if (!modsToml.exists()) {
            throw new GradleException("Generated mods.toml was not found at ${modsToml}")
        }

        def content = modsToml.text
        def missingMandatory = []

        content.split(/\[\[dependencies\./).drop(1).each { block ->
            def dependencyBlock = "[[dependencies.${block}"
            if (!dependencyBlock.contains("mandatory")) {
                missingMandatory << dependencyBlock
            }
        }

        if (!missingMandatory.isEmpty()) {
            throw new GradleException("mods.toml dependencies are missing the mandatory field: ${missingMandatory.join('; ')}")
        }
    }
}

tasks.named("processResources").configure { dependsOn validateModMetadata }
tasks.named("shadowJar").configure { dependsOn validateModMetadata }

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier = ""

    relocate 'com.google.gson', 'hu.snowylol.networkjs.libs.gson'
    relocate 'okhttp3', 'hu.snowylol.networkjs.libs.okhttp3'
    relocate 'okio', 'hu.snowylol.networkjs.libs.okio'
    relocate 'net.dv8tion.jda', 'hu.snowylol.networkjs.libs.jda'
    relocate 'com.sun.jna', 'hu.snowylol.networkjs.libs.jna'
    relocate 'javax.annotation', 'hu.snowylol.networkjs.libs.annotations'
    relocate 'com.google.errorprone', 'hu.snowylol.networkjs.libs.errorprone'
    relocate 'com.fasterxml.jackson', 'hu.snowylol.networkjs.libs.jackson'
    relocate 'org.apache.commons.collections4', 'hu.snowylol.networkjs.libs.collections4'

    exclude 'META-INF/maven/**'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

tasks.jar.configure { enabled = false }

// Reobfuscate the shaded jar instead of the default jar task, which is disabled
afterEvaluate {
    tasks.findByName('reobfJar')?.enabled = false

    def reobfShadowJarTask = tasks.findByName('reobfShadowJar')
    reobfShadowJarTask?.dependsOn shadowJar
    if (reobfShadowJarTask != null) {
        tasks.build.dependsOn reobfShadowJarTask
    }
}

// Use shadowJar as the main artifact
tasks.assemble.dependsOn tasks.shadowJar

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
